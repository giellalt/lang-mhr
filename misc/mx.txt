!IDEAS FOR IMPROVEMENT
!Fsm:ssä pitää erottaa z ja Z omaksi konsonanttiryhmäkseen niiden erikoisen 
!käyttäytymisen takia (niiden jälkeen tulevat soinnittomat suffiksit). Samoin ehkä myös 
!soinnittomat sibilantit (vaativat joskus välivokaalin). 
!COMMENTS ON OVERPRODUCTIVITY
!- generates PxSg3 forms ending in -Z  without restrictions (because a postposition 
!  requiring the short PxSg3 might be the next word). Informants test show no clear
!  rules of the acceptability of the shorter form, see journal 9.2.05
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Polku leksikkoketjun läpi menee pääpiirteissään seuraavassa järjestyksessä:
! LEXICON Root -> (haarautuminen sanaluokkiin) 
!                        -> Stem-alkuiset leksikot (vars. sanasto)
!                        -> N, N-ae jne. (prefiksittömät leksikkonimet)
!                        -> (haarautuminen suffiksileksikoihin)
!                        -> Sx-alkuiset leksikot
!Leksikot ovat tiedostossa suunnilleen samassa järjestyksessä kuin niiden
!läpi mennään eli sisällysluettelo on:

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!CONTENTS OF THE FILE!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (1) MULTICHARACTER SYMBOLS  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (2) INITIAL BRANCHING !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (3) FURTHER BRANCHING ACCORDING TO THE DECLENSIONAL CLASS !!!!
!!!! (4) LEXICONS CONTAINING THE WORDS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (5) LEXICONS PROVIDING THE WORD-CLASS TAGS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (6) BRANCHING INTO THE SUFFIX LEXICONS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (7) LEXICONS CONTAINING THE SUFFIXES !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (8) THE END OF THE WORD !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Principles of the mnemonic naming of lexicons: 
! -- category names begin with capital letter, e.g. LEXICON Cx-GenAcc 
!(a lexicon containing the Genetive and Accusative  case endings)
! -- graphemes represented by small letters, e.g. LEXICON Enc-la ;  Enc-laj
!(a lexicon containing the enclitics la and laj; in some cases the user just must
! know whether, for instance, "y"  refers to "ы" or "ӱ")
! -- classes of graphemes marked by capital letters, e.g. LEXICON StemV2-(C)V
!(a lexicon containing the stems of monosyllabic II conj. verbs with the structure 
!  'a possible consonant and a vowel')
! -- "logical operations" marked by capital letters, e.g. LEXICON Cx-IllIne-NOsh
!(a lexicon containing the Illative and Inessive case suffixes but with no sh grapheme)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (1) MULTICHARACTER SYMBOLS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!  
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

!Flag diacritics
! @P.OBLCASE.PRESENT@   !Oblique case suffix is present
! @D.OBLCASE@  !Disallows the presence of an oblique case suffix
! @U.PX.ZE@ !Controls the use of the abbreviate Sg3 possessive suffix Z
! @U.PX.Z@ !Controls the use of the abbreviate Sg3 possessive suffix Z
! @U.PX.Z@ !Controls the use of the abbreviate Sg3 possessive suffix Z
! @R.PX.Z@ !Controls the use of the abbreviate Sg3 possessive suffix Z
 @P.PXSG3.PRESENT@ !Controls the use of the mozho enclitic and GerAnt mekshe
 @R.PXSG3.PRESENT@ !Controls the use of the mozho enclitic and GerAnt mekshe
 @P.PXSG2.PRESENT@ !Controls the use of the mochet enclitic
 @R.PXSG2.PRESENT@ !Controls the use of the mochet enclitic
 @P.PL.PRESENT@ !Prevents rak-Pl forms
 @D.PL.PRESENT@ !Prevents rak-Pl forms
 @P.GERSIM.BEG@ !Controls the -la of the +Ger+Sim
 @R.GERSIM.BEG@ !Controls the -la of the +Ger+Sim
 @D.GERSIM.BEG@ !Prevents modals in +Ger+Sim
 @P.GERSIM.END@ !Prevents modals in +Ger+Sim
 @P.VERB.PRESENT@ !Controls the suffixes after infinitive verb forms
 @D.VERB.PRESENT@ !Controls the suffixes after infinitive verb forms

!Word classes:
+Cat +N +A +Pr +Qu +Pp +Adv +V

!Cases:
+Nom +Gen +Acc +Com +Ill +Ine +Lat +Dat +Mod

!Number:
+Pls +Plw +Pll +Plm +Sg 

!Possessive suffixes:
+PxSg1 +PxSg2 +PxSg3 +PxPl1 +PxPl2 +PxPl3

!Comparation of adjectives:
+Cmp

!Verbs:
+V1 +V2
+Ind+Pres +Ind+Pret1 +Ind+Pret2 +Imp +Des+Pres
+Sg1 +Sg2 +Sg3 +Pl1 +Pl2 +Pl3
+CoNeg  !Connegative form 
+Inf +Inf+Nec
+Ptc+Act +Ptc+Pas +Ptc+Fut +Ptc+Neg
+Ger+Instr +Ger+Neg +Ger+Ant +Ger+Post +Ger+Sim

!Enclitics:
+Enc

!Symbols for morphophonemes

^E ^A

!Junctures (Tämä tarpeeton?)

!%$

!Symbol for possible epenthetic vowel

VEP

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (2) INITIAL BRANCHING !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root
    Nouns ;
!    Adjectives ;
    Verbs ;
!    Pronouns ;
!    Quantifiers ;
!    Adverbs ;
!    Postpositions ;             
!    Conjunctions ;        
!    Interjections ;     
!    Particles ;
!    Descriptive_words ;
!    Unknown_continuation ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (3) FURTHER BRANCHING ACCORDING TO THE DECLENSIONAL CLASS !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Nouns
    StemN ;
    StemN-ae ;
    StemN-Vs ;
!    StemProp ; 
 !   StemNUnprod ;
 !   StemNUndecl ;

!LEXICON Adjectives
 !   StemA ;
 !   StemA-ae ;
 !   StemA-Vs1 ;
 !   StemAUnprod ;
 !   StemAUndecl ;

!LEXICON Pronouns
!    StemPron ;
 !   StemPronV ;
 !   StemPronEnc ;
 !   StemPronUndecl ;

!LEXICON Quantifiers
!    StemQu ;
!    StemQuUndecl ;

!LEXICON Adverbs
 !   StemAdv ;
 !   StemAdvPx ;
 !   StemAdvCx ;
 !   StemAdvUndecl ;

!LEXICON Postpositions
!    StemPp ;
 !   StemPpCx ;
 !   StemPpPx ;
 !   StemPpPx+ ;
 !   StemPpUndecl ;

LEXICON Verbs
    StemV1 ;
    StemV1-CV ;
    StemV1-j ;
    StemV1-Polysyll ;
    StemV1-nl ;
    StemV1-nlPolysyll ;
    StemV1-k_0 ;
    StemV1-nch_ch ;
    StemV1-t_0 ;
    StemV1-z_ch ;
    StemV2 ; 
    StemV2-j(V) ;
    StemV2-(C)V ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (4) LEXICONS CONTAINING THE WORDS !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!Nouns

LEXICON StemN    
!шомак N ;
куд^E N ;
кеч^E N ;
келг^E N ;
кепк^A N ;
кож N ;
кугу N ;
мун^E N ;
вӱд N ;
рӱд^E N ;
корн^E N ;
тер N ;

LEXICON StemN-ae  
олма N-ae ;
шопке N-ae ;
ава N-ae ;
ола N-ae ;
кыша N-ae ;

LEXICON StemN-Vs 
мучаш N-Vs ;

!LEXICON StemNUnprod    !Nouns with unproductive declensional endings
!s1omak NUnprod	;

!LEXICON StemNUndecl  !Nouns with (almost) no inflection
!s1omak NUndecl	;

!Adjectives

!LEXICON StemA  !This and the following groupings correspond to 
!s1omak A	;  !those of the nouns 

!LEXICON StemA-ae 
!s1omak A-ae	;

!LEXICON StemA-Vs1 
!s1omak A-Vs1	;

!LEXICON StemAUnprod 
!s1omak AUnprod  ;

!LEXICON StemAUndecl 
!s1omak AUndecl	;

!Pronouns

!LEXICON StemPron     !Pronouns that are inflected as nouns  
!s1omak Pron	;

!LEXICON StemPronV     !Pronouns ending in a stressed vowel  
!s1omak PronV	;

!LEXICON StemPronEnc    !Only PxSg3 or clitic can be attached    
!s1omak PronEnc	;

!LEXICON StemPronUndecl    !No declensional endings   
!s1omak PronUndecl	;


!Quantifiers

!LEXICON StemQu     !The long forms that get inflectional endings    
!s1omak Qu	;

!LEXICON StemQuUndecl   !Undeclinable short forms     
!s1omak QuUndecl	;


!Adverbs

!LEXICON StemAdv   !PxSg3, clitic or -rak can be attached    
!s1omak Adv	;

!LEXICON StemAdvPx   !Get other possessive suffixes than Sg3, as well     
!s1omak AdvPx	;

!LEXICON StemAdvCx     !Get unproductive case suffixes  
!s1omak AdvCx	;

!LEXICON StemAdvUndecl   !No declensional endings    
!s1omak AdvUndecl	;


!Postpositions

!LEXICON StemPp       !Declined like adverbs (PxSg3, clitic or -rak ?)    
!s1omak Pp	;

!LEXICON StemPpCx      !Get unproductive case endings
!s1omak PpCx	;

!LEXICON StemPpPx      !Get other possessive suffixes than Sg3, as well 
!s1omak PpPx	;

!LEXICON StemPpPx+       !Some words having more endings (?)
!s1omak PpPx+	;

!LEXICON StemPpUndecl     !No declensional endings  
!s1omak PpUndecl	;

LEXICON StemV1 
пур V1 ;
йод V1 ;
уж V1 ;
луд V1 ;
курж V1 ;
кӱр V1 ;
шупш V1 ;
ӱж V1 ;
кошт V1 ;
код V1 ;
мушк V1 ;
керт V1 ;
уж V1 ;
поч V1 ;

LEXICON StemV1-CV  !These verbs are completely regular. In the 3rd person orthography
                                  !requires the эш instead of еш. A whole suffix series has been
                                  !created for this (V1x-Pres-e).
му V1-CV ;
шу V1-CV ;
пу V1-CV ;

LEXICON StemV1-j  !These verbs are completely regular. The variants are motivated by
                               !the orthography: /j/ is expressed either by j or ja (in the latter case
                               !j and the following a are merged.
шӱй:шӱ V1-(j) ;
шӱй V1-j ;
лий:ли V1-(j) ;
лий V1-j ;

LEXICON StemV1-Polysyll 
ошем V1-Polysyll ;
кораҥ V1-Polysyll ;
колышт V1-Polysyll ;
тунем V1-Polysyll ;
почылт V1-Polysyll ;
шогылт V1-Polysyll ; !olla, lorvia

LEXICON StemV1-nl 
кел V1-nl ;
тол V1-nl ;
кол V1-nl ;
нал V1-nl ;

LEXICON StemV1-nlPolysyll 
тӱҥал V1-nlPolysyll ;
тӧрштыл V1-nlPolysyll  ;
ончал V1-nlPolysyll ;
курал V1-nlPolysyll ; !Kyntää

LEXICON StemV1-k_0 
кушк:куш V1-InitC ;
кушк V1-InitV ;
мушк:муш V1-InitC ;
мушк V1-InitV ;
пӱчк:пӱч V1-InitC ;
пӱчк V1-InitV ;

LEXICON StemV1-nch_ch 
шинч:шич V1-InitC ;
шинч V1-InitV ;
шӱгынч:шӱгыч V1-InitC ; !kuoria, särkeä (pähkin.)
шӱгынч V1-InitV ;

LEXICON StemV1-t_0 
лект:лек V1-InitC ;
лект V1-InitV ;
лукт:лук V1-InitC ;
лукт V1-InitV ;

LEXICON StemV1-z_ch
воз:воч V1-InitC ;
воз V1-InitV ;

LEXICON StemV2  
шул^E V2 ;
йог^E V2 ;
кол^E V2 ;
пур^E V2 ;
пышт^E V2 ;
ург^E V2 ;
воз^E V2 ;
лӱҥг^E V2 ; !heilua, keinua
йӧрат^E V2 ;
мошт^E V2 ;
ру^E V2 ; !Does this analyse correctly
шог^E V2 ;  
ньыкт^E V2 ;  !nylkeä
кеч^E V2 ;  !riippua
онч^E V2 ;  
шон^E V2 ;  
ойл^E V2 ;  
кудалт^E V2 ;  
ойырл^E V2 ;  
сеҥ^E V2 ;  
ышт^E V2 ;
вол^E V2 ;

!This type divided into three continuation class types. The first is motivated by 
!orthography (otherwise only -j and -jY types would be needed). The second is
!the exceptional consonant stem type. The third is needed for the suffixes begin-
!ning with a consonant (most of these do not trigger the epenthesis rule; the final
!vowel of the stem of the 2. conj. is not an epenthetic vowel. In some forms, e.g.
!ImpPl3, the difference between cons. and vow. stems is lost; in these cases
!the model produces two overtly identical analyses, e.g. ka^E+V2+Imp+Pl3.
LEXICON StemV2-j(V)  !Ks. AA85, 105-06 alav.
ка^E V2-je ;  !The ^E morphophoneme seems here odd, but the vowel 
                       !harmony rules produce the right result by inserting e.
                    !Other alternative: stem ka (for presens etc.) and stem kae
                    !for CoNeg and Imp. The present solution is simpler.
ка^E:кай V2-j ;
ка^E:кайы V2-jy ;
ми^E V2-je ;  
ми^E:мий V2-j ;
ми^E:мийы V2-jy ;
лӱ^E V2-je ;  
лӱ^E:лӱй V2-j ;  
лӱ^E:лӱйы V2-jy ;  
чи^E V2-je ;  
чи^E:чий V2-j ;
чи^E:чийы V2-jy ;

LEXICON StemV2-(C)V
ку V2-(C)V ; !'kutoa'; 
ку:ку^E V2-(C)V^E ; !'kutoa'; 
шу V2-(C)V ; 
шу:шу^E V2-(C)V^E ; 
пу V2-(C)V ; 
пу:пу^E V2-(C)V^E ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (5) LEXICONS PROVIDING THE WORD-CLASS TAGS !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!Nouns

LEXICON N
+N+Nom:0 AfterN ;

LEXICON N-ae
+N+Nom:0 AfterN-ae ;

LEXICON N-Vs
+N+Nom:0 AfterN-Vs ;
+N+Ill:0 WordEnd ; !Cont. to WordEnd because the abbreviated form probably cannot occur
                            !in the middle of the form; cont. to enclitics could be added if necessary.

!LEXICON NUnprod
!+N: AfterNUnprod ;

!LEXICON NUndecl  
!+N: AfterNUndecl ;


!Adjectives

!LEXICON A
!+A: AfterN ;

!LEXICON A-ae
!+A: AfterN-ae ;

!LEXICON A-Vs1
!+A: AfterN-Vs1 ;

!LEXICON AUnprod
!+A: SxAUnprod ;

!LEXICON AUndecl  
!+A: SxAUndecl ;

!Pronouns

!LEXICON Pron    
!+Pr:   AfterN       ;

!LEXICON PronV    
!+Pr:   SxPronV       ;

!LEXICON PronEnc    
!+Pr:   SxPronEnc       ;

!LEXICON PronUndecl    
!+Pr:   SxPronUndecl       ;


!Quantifiers

!LEXICON Qu        
!+Qu:   SxQu       ;

!LEXICON QuUndecl        
!+Qu:   SxQuUndecl       ; 


!Adverbs

!LEXICON Adv         
!+Adv:   SxAdv       ;

!LEXICON AdvPx       
!+Adv:   SxAdvPx       ;

!LEXICON AdvCx        
!+Adv:   SxAdvCx       ;

!LEXICON AdvUndecl        
!+Adv:   SxAdvUndecl       ;


!Postpositions

!LEXICON Pp         
!+Pp:   SxAdv       ;

!LEXICON PpCx        
!+Pp:   SxPpCx       ;

!LEXICON PpPx        
!+Pp:   SxPpPx       ;

!LEXICON PpPx+       
!+Pp:   SxPpPx+       ;

!LEXICON PpUndecl       
!+Pp:   SxPpUndecl       ;

!Verbs

LEXICON V1 
+V1:0 AfterV1 ;

LEXICON V1-CV 
+V1:0 AfterV1-CV ;

LEXICON V1-(j) 
+V1:0 AfterV1-(j) ;

LEXICON V1-j
+V1:0 AfterV1-j ;

LEXICON V1-Polysyll 
+V1:0 AfterV1-Polysyll ;

LEXICON V1-nl 
+V1:0 AfterV1-nl ;

LEXICON V1-nlPolysyll 
+V1:0 AfterV1-nlPolysyll ;

LEXICON V1-InitV 
+V1:0 AfterV1-InitV ;

LEXICON V1-InitC 
+V1:0 AfterV1-InitC ;

LEXICON V2
+V2:0 AfterV2 ;

LEXICON V2-je 
+V2:0 AfterV2-je ;

LEXICON V2-j
+V2:0 AfterV2-j ;

LEXICON V2-jy
+V2:0 AfterV2-jy ;

LEXICON V2-(C)V
+V2:0 AfterV2-(C)V ;

LEXICON V2-(C)V^E
+V2:0 AfterV2-(C)V^E ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (6) BRANCHING INTO THE SUFFIX LEXICONS !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! 

LEXICON AfterN 
  Cx-Gen ; Cx-Acc ; Cx-Com ;  Cx-Ill ; Cx-Ine ; Cx-Lat-e ; Cx-Dat ; Cx-Mod ;  
  Px-Sg12-e ; Px-Sg3-zhe ; Px-Sg3-zh ; Px-Pl ;
  Pl-sh_w ;  Pl-la ;  Pl-myt ;
  Cmp ; !the rak ending must later be restricted to adjectives
  Enc-ak ; Enc-at ; Enc-la ;  Enc-laj ; Enc-s ; Enc-ys ; !makes the encl. (-)(y)s probably 
                                                                               !overproductive
  WordEnd ;

LEXICON AfterN-ae
  Cx-Gen ; Cx-Acc ; Cx-Com ; Cx-Ill ; Cx-Ine ; Cx-Lat-NOe ; Cx-Dat ; Cx-Mod ;  
  Px-Sg12-NOe ; Px-Sg3-zhe ; Px-Sg3-zh ; Px-Pl ;
  Pl-sh_w ;  Pl-la ;  Pl-myt ;
  Cmp ; !the rak ending must later be restricted to adjectives
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ; Enc-s ; Enc-ys ; !makes the encl. (-)(y)s probably 
                                                                              !overproductive
   WordEnd ; 

LEXICON AfterN-Vs
  Cx-Gen ; Cx-Acc ; Cx-Com ; Cx-Ill ; Cx-Ine ; Cx-IllIne-NOsh ;  Cx-Lat-e ;  Cx-Dat ; Cx-Mod ;  
  Px-Sg12-e ; Px-Sg3-zhe ;  Px-Sg3-zh ; Px-Pl ;
  Pl-sh_w ;  Pl-la ;  Pl-myt ;
  Cmp ; !the rak ending must later be restricted to adjectives
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ; Enc-s ; Enc-ys ; !makes the encl. (-)(y)s probably 
                                                                              !overproductive
  WordEnd ; 
!-----------------------------------------------------------------------------------------------------------
 LEXICON AfterCx-End  
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ; !Ac1amyn-laj
  WordEnd ; 

LEXICON AfterCx-Px  
  Px-Sg12-e ; Px-Sg3-zhe ; Px-Pl ; 
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ; !other enclitics to be added when encountered
  WordEnd ; 

LEXICON AfterCx-aPx  
  Px-Sg12-NOe ; Px-Sg3-zhe ;  Px-Pl ;
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ; !other enclitics to be added when encountered
  WordEnd ; 

LEXICON AfterCx-Lat  
  Px-Sg12-e ; Px-Sg3-yzhe ; Px-Pl ; 
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ;  !other enclitics to be added when encountered
  WordEnd ; 
 !----------------------------------------------------------------------------------------------------------- 
LEXICON AfterPx 
  Cx-Gen ; Cx-Acc ; Cx-Com ; Cx-Dat ; Cx-Mod ;
  Pl-sh_w ;  !Pl-la not included because of the dominant Pl-Px order. Perhaps    Pl-myt 
           !should be added later if there appears Px-PlmYt forms.
  Vx-GerSim-la ;
  Enc-ak ; Enc-at ;  Enc-la ;  Enc-laj ; Enc-mozho ; Enc-mochet ;   !other enclitics to be added 
                                                                                !when encountered
  WordEnd ;
 
LEXICON AfterPx-zh 
  Cx-Com ; Cx-Dat ; Cx-Mod ;
  Vx-GerSim-la ;
  WordEnd ; 
!-----------------------------------------------------------------------------------------------------------
!The continuations of the plurals are constructed so that vlak and samych take
!after them anything that a normal word does, whereas la takes only local cases
! and myt takes other cases than local.
LEXICON AfterPl-la 
  Cx-Ill ; Cx-Ine ; Cx-Lat-NOe ;  !Other cases must be added if found in texts
  Px-Sg12-NOe ; Px-Sg3-zhe ;  Px-Pl ;
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ;  !other enclitics to be added when encountered
  WordEnd ; 

LEXICON AfterPl-myt 
  Cx-Gen ; Cx-Acc ; Cx-Com ;  Cx-Dat ; Cx-Mod ;  !Local cases must be added if found in texts
  Px-Sg12-e ; Px-Sg3-zhe ;  Px-Pl ;
  Enc-ak ; Enc-at ; Enc-la ; Enc-laj ; 
  WordEnd ;
!---------------------------------------------------------------------------------------------------------------------------- 
LEXICON AfterCmp  !Based on the distribution in Onchyko corpus
  Cx-Gen ; Cx-Acc ; Cx-Dat ; Cx-Mod ;
  Px-Sg12-e ; Px-Sg3-zhe ; Px-Pl ;
  Pl-sh_w ;
  Enc-ak ; Enc-at ;  Enc-la ; Enc-laj ;
  WordEnd ; 
!----------------------------------------------------------------------------------------------------------------------------
LEXICON AfterV1 
  Vx-CoNeg ; 
  V1x-Pres ; 
  V1x-Pret1SgPl3 ; V1x-Pret1Pl12 ;
  V1x-Pret2 ;
  Vx-ImpSg2 ; Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-y ;

LEXICON AfterV1-CV 
  Vx-CoNeg ; 
  V1x-Pres-e ; 
  V1x-Pret1SgPl3 ; V1x-Pret1Pl12 ;
  V1x-Pret2 ;
  Vx-ImpSg2 ; Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-y ;

LEXICON AfterV1-(j)  !These verbs are completely regular. The two suffix lexicons are
                                !needed for orthographical reasons
  V1x-PresSg-j ;  

LEXICON AfterV1-j   !These verbs are completely regular. The two suffix lexicons are
                                !needed for orthographical reasons
  Vx-CoNeg ; 
  V1x-PresPl-j ;
  V1x-Pret1SgPl3 ; V1x-Pret1Pl12 ;
  V1x-Pret2 ; 
  Vx-ImpSg2 ; Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-y ;

LEXICON AfterV1-Polysyll 
  Vx-CoNeg ; 
  V1x-Pres ; 
  V1x-Pret1SgPl3 ; V1x-Pret1Pl12 ;
  V1x-Pret2 ;
  Vx-ImpSg2 ; Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-y ; Vx-GerInstr-0 ;

LEXICON AfterV1-nl 
  Vx-CoNeg ; 
  V1x-Pres ; 
  V1x-PalatMark ; V1x-Pret1Pl12 ; 
  V1x-Pret2 ;
  Vx-ImpSg2 ; Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-y ;

LEXICON AfterV1-nlPolysyll 
  Vx-CoNeg ; 
  V1x-Pres ; 
  V1x-PalatMark ; V1x-Pret1Pl12 ; 
  V1x-Pret2 ;
  Vx-ImpSg2 ; Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-y ; Vx-GerInstr-0 ;

LEXICON AfterV1-InitV 
  V1x-Pres ; V1x-Pret1SgPl3 ; V1x-Pret2 ; 
  Vx-Inf ; Vx-GerInstr-y ;

LEXICON AfterV1-InitC 
  Vx-CoNeg ; V1x-Pret1Pl12 ; Vx-ImpSg2 ;  Vx-ImpSg3Pl ; Vx-Des ; 
  Vx-InfNec ; Vx-Ptc ; Vx-Ger ; 

LEXICON AfterV2 
  Vx-CoNeg ; 
  V2x-Pres ; 
  V2x-Pret1 ; 
  V2x-Pret2 ; 
  Vx-ImpSg2 ; Vx-ImpSg3Pl  ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-je ;

LEXICON AfterV2-je 
  Vx-CoNeg ; 
  V2x-Pres-j ; 
  V2x-Pret2 ; 
  Vx-ImpSg2 ; 
  Vx-Inf-j ; Vx-GerInstr-je ; 

!Consonant stems used in the older othography, see AA85, 105-06 footnote.
!The model produces forms based on cons. stems of all the VjV-type verbs.
!It is not clear if all the cons. stem based forms have actually occurred in
!the written language.
LEXICON AfterV2-j 
  Vx-CoNeg ; 
  Vx-ImpSg2 ; 
!The following represent the exceptional old orthography forms, s. AA85:
! 105-106 footnote:
  Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-InfNec ; Vx-Ptc ; Vx-Ger ;

LEXICON AfterV2-jy 
  V2x-Pret1; 
  Vx-ImpSg3Pl ; 
  Vx-Des ;
  Vx-InfNec ; Vx-Ptc ; Vx-Ger ;

LEXICON AfterV2-(C)V^E  
  V2x-Pres-e ;                 
  V2x-Pret1 ;
  V2x-Pret2-e ;
  Vx-ImpSg3Pl ;
  Vx-Des ;
  Vx-Inf ; Vx-InfNec ; Vx-Ptc ; Vx-Ger ; Vx-GerInstr-e ;

LEXICON AfterV2-(C)V  !The shorter variant (C)V only in two categories
  Vx-CoNeg ;                  
  Vx-ImpSg2 ;
!----------------------------------------------------------------------------------------------------------------------------
LEXICON AfterVxPers  !From Pret1 no cont.; to be added later, if cases encountered
  Enc-at ; !The func. of at here is that of a conjunction; at might not appear in all tenses, 
              !the continuation may thus be overproductive; ak to be added when encountered
  Enc-la ; Enc-laj ; Enc-ogyla ;  !ogyla probably not used in Pret1? For the 
                                                            !time being there is no cont. to this lex. 
                                                            !from Pret1 forms.
  Enc-ys ; !The distributional difference with -s somewhat unclear; if -s is the variant after
               !vowels, e.g., +Ind+Pres+Pl12 and +Ind+Pret2+Pl12 should have -s.
 WordEnd ;

LEXICON AfterVxImp
 Enc-ja_jan ;
 WordEnd ;

!Infiniittimuotojen kontinuaatioita koodatessa on aina pantu kont. kaikkiin 
!px:ihin, vaikka esimerkeissä olisi tavattu vain jokin niistä. Näin ollen 
!malli voi olla yliproduktiivinen px:ien liittymisen osalta. Sen sijaan 
!sijoista on merkitty vain ne, jotka todella ovat esiintyneet esimerkeissä.
!Kontinuaatiot ovat todennäköisesti myös muutenkin yliprodusoivia, sillä
!possessiivisuffikseista ja sijoista on normaalit, tavallisten nominien
!kontinuaatiot, vaikka ilmeisesti vain osa näistä on todellisuudessa 
!kielessä esiintyviä muotoja. Käytännöllinen tapa rajoittaa produktiivi-
!suutta olisi luoda oma sarja px:ja verbimuodoille, mutta tämä johtaisi
!periaatteellisiin ongelmiin, koska kysymys on kuitenkin samoista pos-
!sessiivisuffikseista kuin nomineillakin. Lippudiakriitein tapahtuva pro-
!duktiivisuuden rajoittaminen vaatisi suuren määrän diakriittejä, koska
!tietty suffiksi voi esiintyä tietyn infiniittimuodon kanssa, mutta toinen
!läheinen infiniittimuoto taas ei. Sääntösysteemiin liittyy iherentisti
!rajoittamiaton produktiivisuus, jonka käytännön rajoittaminen on vaikeaa.
!Lisälähde (9.5.05): Bereczki 2002: A cseremisz nyelv történeti alaktana, s. 117- 
!(sijasuffiksillisia infiniittimuotoja; tarkistettava, että kaikki Bereczkin
!esittämät muodot analysoituvat mallissa). 
LEXICON AfterVx-Inf
 Cx-Dat ; 
 Px-Sg12-e ; Px-Sg3-yzhe ; Px-Pl ; 
 Enc-ak ; Enc-at ;
 WordEnd ;

LEXICON AfterVx-InfNec
 Enc-ak ; Enc-at ; Enc-s ;
 WordEnd ;

LEXICON AfterVx-PtcAct
 Cx-Gen ; Cx-Acc ; Cx-Dat ; !Only one, not fully understood ex. of Gen
 Px-Sg12-e ; Px-Sg3-zhe ; Px-Sg3-zh ; Px-Pl ; 
 Enc-at ; !at is based on some unclear examples, must be checked
 WordEnd ;

LEXICON AfterVx-PtcPas
 Cx-Acc ; Cx-Dat ; Cx-Mod ; !Check that there are real modals, not only mYla
                                          !gerunds.
 Px-Sg12-e ; Px-Sg3-zhe ; Px-Sg3-zh ; Px-Pl ; 
 WordEnd ;

LEXICON AfterVx-PtcFut
 Cx-Acc ; Cx-Dat ; Cx-Ill ; Cx-Mod ; 
 Px-Sg12-e ; Px-Sg3-yzhe ; Px-Sg3-zh ; Px-Pl ; 
 WordEnd ;

LEXICON AfterVx-PtcNeg
 Cx-Acc ; Cx-Gen ; Cx-Dat ; Cx-Ill ; Cx-Mod ; 
 Px-Sg12-e ; Px-Sg3-zhe ;Px-Sg3-zh ; Px-Pl ; 
 Pl-sh_w ; !Only vlak attested
 Cmp ;
 WordEnd ;

LEXICON AfterVx-GerNeg
 Px-Sg12-NOe ; Px-Sg3-zhe ; Px-Pl ; 
 WordEnd ;

LEXICON AfterVx-GerAnt
 Px-Sg12-e ; Px-Sg3-zhe ; Px-Pl ; 
 Enc-ak ; Enc-at ;
 WordEnd ;

LEXICON AfterVx-GerPost
 Px-Sg12-e ; Px-Sg3-zhe ; Px-Pl ; 
 Enc-ak ; Enc-at ;
 WordEnd ;

LEXICON AfterVx-GerPost-short
 Enc-at ;
 WordEnd ;

LEXICON AfterVx-GerSim-syn
 Enc-ak ;
 WordEnd ;

!Not all pxs have been attested in GerSim gerunds; may be overproductive
LEXICON AfterVx-GerSim-sy-
 Px-Sg12-e ; Px-Sg3-zh ; Px-Pl ; 
 Vx-GerSim-la ;

LEXICON AfterVx-GerSim-my-
 Px-Sg12-e ; Px-Sg3-zh ; Px-Pl ; 
 Vx-GerSim-la ;

LEXICON AfterVx-GerSim-la
 Px-Sg12-NOe ; Px-Sg3-zhe ; Px-Pl ; 
 WordEnd ;

LEXICON AfterEnc-ak 
 Enc-la ;
 WordEnd ;

LEXICON AfterEnc-at  
 Enc-laj ;
 WordEnd ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (7) LEXICONS CONTAINING THE SUFFIXES !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!Huom! Kaikkien suffiksien eteen pitää panna 
!!!junktuurisymboli $, jotta morfofonologiset 
!!!säännöt toimisivat oikein.

!Nouns

!The flag diacritics @U.PX... can be removed by placing Comitative in a 
!separate lexicon and adjusting the continuation class in the second line
!of lexicon Px-Sg3.
!-------------------------------------------------------------------------------------------------------------------
LEXICON Cx-Gen 
< "+Gen":[%$ VEP н]  >  AfterCx-End;  !no pxs after this

LEXICON Cx-Acc  
< "+Acc":[%$ VEP м]  >  AfterCx-End;  !no pxs after this 

LEXICON Cx-Com
< "+Com":[%$ г е]  >  AfterCx-End; !This lexicon is needed because of
                                                    !the shorter variant of the PxSg3
                                                    !which is attached to Dat, Mod,Com.

LEXICON Cx-Ill   !Ill and Ine divided into two lexicons because only Ill
                              !can come after -SaS participe
< "+Ill":[%$ VEP ш к %^E]  >  AfterCx-Px;  !normal pxs 
< "+Ill":[%$ VEP ш]  >  AfterCx-End; !Used only without Px 

LEXICON Cx-Ine
< "+Ine":[%$ VEP ш т %^E]  >  AfterCx-Px;

LEXICON Cx-IllIne-NOsh
< "+Ill":[%$ к %^E]  > AfterCx-Px; 
< "+Ine":[%$ т %^E]  > AfterCx-Px; 

LEXICON Cx-Lat-NOe
< "+Lat":[%$ ш]  > AfterCx-Lat ; !*muneSSe type  prevented 

LEXICON Cx-Lat-e
< "+Lat":[%$ е ш]  > AfterCx-Lat;

LEXICON Cx-Dat
< "+Dat":[%$ л а н]  > AfterCx-Px;

LEXICON Cx-Mod
< "+Mod":[%$ л а]  "@D.GERSIM.BEG@" > AfterCx-aPx; !Pxs that come 
                                                     !after strong a/e
!-------------------------------------------------------------------------------------------------------------------
LEXICON Px-Sg12-e
+PxSg1:$ем AfterPx ;
< "+PxSg1+Acc":[%$ е м]  "@D.VERB.PRESENT@" > AfterPx ;
< "+PxSg2":[%$ е т]  "@P.PXSG2.PRESENT@" > AfterPx ;

LEXICON Px-Sg12-NOe
+PxSg1:$м AfterPx ;
+PxSg1+Acc:$м AfterPx ;
< "+PxSg2":[%$ т]  "@P.PXSG2.PRESENT@" > AfterPx ;

LEXICON Px-Sg3-zhe
< "+PxSg3":[%$ VEP ж %^E]  "@P.PXSG3.PRESENT@" > AfterPx ; 

LEXICON Px-Sg3-zh
< "+PxSg3":[%$ VEP ж]  > AfterPx-zh ;  !only in certain cases

LEXICON Px-Sg3-yzhe    !This lexicon is for the latives, to expel the 
                                   !*muneSSe type
< "+PxSg3":[%$ ы ж %^E]  "@P.PXSG3.PRESENT@" > AfterPx ; 

LEXICON Px-Pl
+PxPl1:$на AfterPx ;
+PxPl2:$да AfterPx ;
+PxPl3:$VEPшт AfterPx ;
!-------------------------------------------------------------------------------------------------------------------
LEXICON Pl-sh_w  !shamych and wlak plurals are treated (almost) like normal words
< "+Pls":[%$ %- ш а м ы ч]  "@P.PL.PRESENT@" > AfterN ;
< "+Plw":[%$ %- в л а к]  "@P.PL.PRESENT@" > AfterN ;

LEXICON Pl-la
+Pll:$ла AfterPl-la ; !pitää laittaa kontinuaatio pxeihin, sillä PlPx tässä 
                                !dominoiva, 
                               !ks. väikkäri s. 125

LEXICON Pl-myt
+Plm:$мыт AfterPl-myt ; 
!-------------------------------------------------------------------------------------------------------------------
LEXICON Cmp
< "+Cmp":[%$ р а к ]  "@D.PL.PRESENT@"  > AfterCmp ;  !Assumed that there are 
                                                                                       !no comparatives from plurals
!-------------------------------------------------------------------------------------------------------------------
LEXICON Vx-CoNeg
+CoNeg:0 WordEnd ;

LEXICON V1x-Pres
+Ind+Pres+Sg1:$ам AfterVxPers ;
+Ind+Pres+Sg2:$ат AfterVxPers ;
+Ind+Pres+Sg3:$еш AfterVxPers ;
+Ind+Pres+Pl1:$ына AfterVxPers ;
+Ind+Pres+Pl2:$ыда AfterVxPers ;
+Ind+Pres+Pl3:$ыт AfterVxPers ;

LEXICON V1x-Pres-e
+Ind+Pres+Sg1:$ам AfterVxPers ;
+Ind+Pres+Sg2:$ат AfterVxPers ;
+Ind+Pres+Sg3:$эш AfterVxPers ;
+Ind+Pres+Pl1:$ына AfterVxPers ;
+Ind+Pres+Pl2:$ыда AfterVxPers ;
+Ind+Pres+Pl3:$ыт AfterVxPers ;

LEXICON V1x-PresSg-j  !This is motivated only for the orthography
+Ind+Pres+Sg1:$ям AfterVxPers ;
+Ind+Pres+Sg2:$ят AfterVxPers ;
+Ind+Pres+Sg3:$еш AfterVxPers ;

LEXICON V1x-PresPl-j !This is motivated only for the orthography
+Ind+Pres+Pl1:$ына AfterVxPers ;
+Ind+Pres+Pl2:$ыда AfterVxPers ;
+Ind+Pres+Pl3:$ыт AfterVxPers ;

LEXICON V2x-Pres
+Ind+Pres+Sg1:$ем AfterVxPers ;
+Ind+Pres+Sg2:$ет AfterVxPers ;
+Ind+Pres+Sg3:$а AfterVxPers ;
+Ind+Pres+Pl1:$ена AfterVxPers ;
+Ind+Pres+Pl2:$еда AfterVxPers ;
+Ind+Pres+Pl3:$ат AfterVxPers ;

LEXICON V2x-Pres-j
+Ind+Pres+Sg1:$ем AfterVxPers ;
+Ind+Pres+Sg2:$ет AfterVxPers ;
+Ind+Pres+Sg3:$я AfterVxPers ;
+Ind+Pres+Pl1:$ена AfterVxPers ;
+Ind+Pres+Pl2:$еда AfterVxPers ;
+Ind+Pres+Pl3:$ят AfterVxPers ;

LEXICON V2x-Pres-e     !For (C)V monosyllabic 2. conj. verbs; orthography requires this
+Ind+Pres+Sg1:$эм AfterVxPers ;
+Ind+Pres+Sg2:$эт AfterVxPers ;
+Ind+Pres+Sg3:$а AfterVxPers ;
+Ind+Pres+Pl1:$эна AfterVxPers ;
+Ind+Pres+Pl2:$эда AfterVxPers ;
+Ind+Pres+Pl3:$ат AfterVxPers ;

LEXICON V1x-Pret1SgPl3
+Ind+Pret1+Sg1:$ым WordEnd ;
+Ind+Pret1+Sg2:$ыч WordEnd ;
+Ind+Pret1+Sg3:$^E WordEnd ;
+Ind+Pret1+Pl3:$ыч WordEnd ;

LEXICON V1x-PalatMark
0:ь V1x-Pret1SgPl3 ;

LEXICON V1x-Pret1Pl12 
+Ind+Pret1+Pl1:$на WordEnd ;
+Ind+Pret1+Pl2:$да WordEnd ;

LEXICON V2x-Pret1
+Ind+Pret1:$ш V2x-Pret1Pers ; 

LEXICON V2x-Pret1Pers
+Sg1:$ым WordEnd ;
+Sg2:$ыч WordEnd ;
+Sg3:0 WordEnd ;
+Pl1:$на WordEnd ;
+Pl2:$да WordEnd ;
+Pl3:$т WordEnd ;

LEXICON V1x-Pret2
+Ind+Pret2:$ын Vx-Pret2Pers ;

LEXICON V2x-Pret2
+Ind+Pret2:$ен Vx-Pret2Pers ;

LEXICON V2x-Pret2-e   !For (C)V monosyllabic 2. conj. verbs
+Ind+Pret2:$эн Vx-Pret2Pers ;

LEXICON Vx-Pret2Pers
+Sg1:$ам AfterVxPers ;
+Sg2:$ат AfterVxPers ;
+Sg3:0 AfterVxPers ;
+Pl1:$на AfterVxPers ;
+Pl2:$да AfterVxPers ;
+Pl3:$ыт AfterVxPers ;

LEXICON Vx-ImpSg2  !Sg Sx must be in a separate lexicon because (C)V type 
                                  !V2 verbs
+Imp+Sg2:0 AfterVxImp ;

LEXICON Vx-ImpSg3Pl
+Imp+Sg3:$ж^E WordEnd ;
+Imp+Pl2:$за AfterVxImp ;
+Imp+Pl3:$VEPшт WordEnd ; !VEP needed for 1. conjugation

LEXICON Vx-Des
+Des+Pres:$не Vx-DesPers ; !VEP tarpeen; pantu ka^E/kaj-
                                                   !tyypin takia?

LEXICON Vx-DesPers 
+Sg1:$м WordEnd ;
+Sg2:$т WordEnd ;
+Sg3:$ж^E WordEnd ;
+Pl1:$на WordEnd ;
+Pl2:$да WordEnd ;
+Pl3:$шт WordEnd ;

! @P.VERB.PRESENT@ used only to prevent the accusative inter-
!pretation of the PxSg1 like form. The flag diacritic could thus be
!deleted in most cases. Check the current usage before doing that!
LEXICON Vx-Inf  !Division into  infinitive lexicons: initial
                          !V/C in -InitV/-InitC type cont. classes
< "+Inf":[%$ а ш]  "@P.VERB.PRESENT@" > AfterVx-Inf ;

LEXICON Vx-Inf-j  
< "+Inf":[%$ я ш]  "@P.VERB.PRESENT@" > AfterVx-Inf ;

LEXICON Vx-InfNec
+Inf+Nec:$ман AfterVx-InfNec ;

LEXICON Vx-Ptc
< "+Ptc+Act":[%$ ш %^E] "@P.VERB.PRESENT@" > AfterVx-PtcAct ;
< "+Ptc+Pas":[%$ м %^E] "@P.VERB.PRESENT@" > AfterVx-PtcPas ;
< "+Ptc+Fut":[%$ ш а ш] "@P.VERB.PRESENT@" > AfterVx-PtcFut ;
< "+Ptc+Neg":[%$ д ы м %^E] "@P.VERB.PRESENT@" > AfterVx-PtcNeg ;

LEXICON Vx-Ger
< "+Ger+Neg":[%$ д е] "@P.VERB.PRESENT@" > AfterVx-GerNeg ;
< "+Ger+Ant":[%$ м е к %^E] "@P.VERB.PRESENT@" > AfterVx-GerAnt ; !Check that it is really ^E and not e.
< "+Ger+Ant":[%$ м е к] "@P.VERB.PRESENT@" "@R.PXSG3.PRESENT@"> AfterVx-GerAnt ; 
< "+Ger+Post":[%$ м е ш к %^E] "@P.VERB.PRESENT@" > AfterVx-GerPost ; !Check that it is really ^E and not e.
+Ger+Post:$меш AfterVx-GerPost-short ;
+Ger+Sim:$шын AfterVx-GerSim-syn ;
< "+Ger+Sim":[%$ ш %^E] "@P.VERB.PRESENT@" "@P.GERSIM.BEG@" >  AfterVx-GerSim-sy- ;  
< "+Ger+Sim":[%$ м %^E] "@P.VERB.PRESENT@" "@P.GERSIM.BEG@" >  AfterVx-GerSim-my- ;  

LEXICON Vx-GerInstr-je
+Ger+Instr:$ен WordEnd ;

LEXICON Vx-GerInstr-e
+Ger+Instr:$эн WordEnd ;

LEXICON Vx-GerInstr-y !Other altern. would be VEPn, but since there are
                                     !no vowel-final I conj. verb stems, the use of VEP 
                                     !has little motivation.
+Ger+Instr:$ын WordEnd ;

LEXICON Vx-GerInstr-0
+Ger+Instr:0 WordEnd ;

LEXICON Vx-GerSim-la
< "":[%$ л а] "@R.GERSIM.BEG@" "@P.GERSIM.END@" >  AfterVx-GerSim-la ;  

LEXICON Enc-ak  !Generally after any forms of nouns, (and verbs)
+Enc:$ак  AfterEnc-ak ;  !After ak the enclitic -la is attested 

LEXICON Enc-at  !Generally after any forms of nouns, (in verbs has the func. of conjunction)
+Enc:$ат  AfterEnc-at ; !After at the enclitic -laj is attested

LEXICON Enc-la  !Generally after anything; la and laj are different enclitics.
+Enc:$-ла  WordEnd ; 

LEXICON Enc-laj  !Generally after anything; la and laj are different eclitics.
+Enc:$-лай  WordEnd ; 

LEXICON Enc-ja_jan  !A continuation to this lexicon from Sg2 ja Pl2 imperatives;
                                 !ja occurs probably only in Sg2? jan attested in both Sg and Pl.
+Enc:$-я  WordEnd ; 
+Enc:$-ян  WordEnd ;
 
LEXICON Enc-ogyla !A continuation to this lexicon from AfterVxPers, i.e. not from Pret1.  
+Enc:$-огыла  WordEnd ; 

LEXICON Enc-mozho 
!A continuation to this lexicon from PxSg3 (vokaaliloppuiset) ; ala-moZo yms. 
! pronominitapaukset kokonaisina Root-leksikossa
< "+Enc":[%$ %- м о ж о]  "@R.PXSG3.PRESENT@" > WordEnd ; 

LEXICON Enc-mochet
< "+Enc":[%$ %- м о ч е т]  "@R.PXSG2.PRESENT@"  >  WordEnd ; 

!About Enc-s and Enc-ys: there are continuation to these lexicons from nominative nouns, 
!but not from other cases, as the only attested form in the Onchyko corpus is pajremys. 
!The other path to this lex. goes through the pers. endings of Pres and Pret2 (AfterVxPers).
!tevys (<tev^E) is also found, add therefore the required cont. later to the pronouns lexicon. 
!The present system makes (-)(y)s overproductive. Acc. to O.Sergeyev 2.05, (-)ys is used 
!after cons. and (-)s after vowels; however, net-s is found in the corpus. Perhaps the distrib-
!ution should be later defined by the phonological environment (the word ending in -C or -V).
!The vowel in (-)ys could be treated as an epenthetic vowel. A new insertion rule must be
!added or the former one reformulated, because the hyphen prevents the normal rule from
!working.
LEXICON Enc-s !distribution probably narrower
+Enc:$-с  WordEnd ; 
+Enc:$с  WordEnd ; !The orthographic dictionary gives -s and -ys without hyphen; triggers
                              !^E=>Y rule, e.g. tevys (tev^E+s).

LEXICON Enc-ys  !Mostly after the personal suffixes of verbs, but nouns also can have it
+Enc:$-ыс  WordEnd ;  
+Enc:$ыс  WordEnd ; !The orthographic dictionary gives -s and -ys without hyphen


!LEXICON SxNUnprod 
!$x WordEnd	;

!LEXICON SxNUndecl 
!$x WordEnd	;

!Adjectives

!LEXICON SxN  !Tämä siis jo substantiiveissa

!LEXICON SxN-ae !Tämä siis jo substantiiveissa 

!LEXICON SxN-Vs1 !Tämä siis jo substantiiveissa

!LEXICON SxAUnprod 
!$x WordEnd  ;

!LEXICON SxAUndecl 
!$x WordEnd	;

!Pronouns

!LEXICON SxN     !Tämä on jo substantiiveissa  

!LEXICON SxPronV       
!$x  WordEnd	;

!LEXICON SxPronEnc       
!$x  WordEnd	;

!LEXICON SxPronUndecl       
!$x  WordEnd	;


!Quantifiers

!LEXICON SxQu       
!$x  WordEnd	;

!LEXICON SxQuUndecl       
!$x  WordEnd	;


!Adverbs

!LEXICON SxAdv       
!$x  WordEnd	;

!LEXICON SxAdvPx       
!$x  WordEnd	;

!LEXICON SxAdvCx       
!$x  WordEnd	;

!LEXICON SxAdvUndecl       
!$x  WordEnd	;


!Postpositions

!LEXICON SxAdv       !Tämä jo adverbeissa

!LEXICON SxPpCx      
!$x  WordEnd	;

!LEXICON SxPpPx       
!$x  WordEnd	;

!LEXICON SxPpPx+       
!$x  WordEnd	;

!LEXICON SxPpUndecl       
!$x  WordEnd	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!! (8) THE END OF THE WORD !!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON WordEnd
 < "@D.GERSIM.BEG@" >  # ;  !To prevent path from GerSimBEG
                                                ! -> Px -> WordEnd

END



