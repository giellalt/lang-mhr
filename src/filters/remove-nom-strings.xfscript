!KEHITTÄMISMAHDOLLISUUKSIA
!- voisi olla sääntö, joka tekee varbin analyysissa näytettävästä muodosta infinitiivin näköisen, ts. lisää -aS 
!tai -AS. Tämän säännön teko on mutkikasta, koska -AS-variantin esiintyminen riippuu siitä, onko ^E:tä
!ennen konsonantti vai vokaali: jos on vokaali, esim. ka^E, niin tulee -AS, muuten -aS.
!-----------------------------------------------------------------------------------------------------------------

!FILTERS OR RULES WITH SIMILAR EFFECT

!NOMINATIIVISUODATIN. Seuraava poistaa +Nom:n ja +Ill:n, jos sanassa on muihin sijoihin 
!liittyviä tageja.

define CxObl  %+Gen  |  %+Acc  |  %+Com  |  %+Ill  |  %+Ine   |  %+Lat   |  %+Dat  |  %+Mod ; 

define NomDeletion  [ ] <- %+Nom || _ ?* CxObl ;

define IllDeletion  [ ] <- %+Ill || _ ?* CxObl ;

!POSSESSIIVISUFFIKSISUODATIN.
!Suodattaa pois moninkertaiset possessiivisuffiksit. 
!Huom! tätä modifioitava myöh. niin, 
!että definiittistävät px:t tulevat mahdollisiksi muita pxja sisältävän sanan jälkeen. Ehkä 
!tehtävä omat leksikot näille possessiivisuffikseille.

define Px  %+PxSg1  |  %+PxSg2  |  %+PxSg3  |  %+PxPl1  |  %+PxPl2  |  %+PxPl3 ;

define PxFilter ~$[ Px ?* Px ] ;

!OBLIIKVISIJAPÄÄTESUODATIN.
!Suodattaa pois moninkertaiset sijapäätteet. Huom1: Koskee muita kuin nominatiivia, jonka leksikko
!panee alustavasti kaikkiin nomineihin ja jonka tarpeettomat esiintymät poistetaan
!NomDeletion-säännöllä.  Huom2: filtteriä modifioitava myöhemmin, koska
!marissa jossain määrin esiintyy moninikertaista sijataivutusta. 

define CxOblFilter ~$[ CxObl ?* CxObl ] ; 

!The following three filters are due to the treatment of vlak and SamYT plural endings as
!(almost) ordinary words (see the continuations in the lexicon Pl-sh_w).
!MONIKKOSUODATIN.
!Suodattaa pois moninkertaiset monikot

define Plur  %+Pls | %+Plw | %+Pll | %+Plm ;

define PlFilter ~$[Plur ?* Plur] ;

!SIJA-MONIKKO-SUODATIN
!Suodattaa pois yhdistelmät, joissa Plur seuraa Cx:ää. Koska +Nom tulee aina automaatti-
!sesti ennen monikon tunnusta, säännössä ei tarvitse käsitellä nominatiivia.

define CxPlFilter ~$[ CxObl ?* Plur ] ;
 
!RAK-KOMPARATIIVISUODATIN
!Suodattaa pois moninkertaisia rak-päätteitä sisältävät muodot

define CmpFilter ~$[%+Cmp ?* %+Cmp] ;

!-----------------------------------------------------------------------------------------------------------------------------

read regex  
NomDeletion .o. 
IllDeletion .o. 
CxOblFilter .o. 
PxFilter .o. 
PlFilter .o. 
CxPlFilter  .o. CmpFilter;
