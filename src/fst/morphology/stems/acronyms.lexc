!! Eastern Mari acronym file


LEXICON Acronym
acros ;
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ] > acr2 ;
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ] > acr5 ;



!! Here is the list of lexicalised Sem/Org acronym proper nouns 
!! These are also generated by the Acrogenerator

ГИТИС:ГИТИС  acrotag_FrontObstr ;
ДОССАФ+Sem/Org:ДОССАФ  acrotag_FrontObstr ;
КамАЗ:КамАЗ acrotag_FrontSon ;
МарГИЗ+Sem/Org:МарГИЗ acrotag_FrontVowel ;
МарГТУ+Sem/Org:МарГТУ acrotag_BackVowel ;
МарНИИ+Sem/Org:МарНИИ acrotag_FrontVowel ;
МарНИИЯЛИ+Sem/Org:МарНИИЯЛИ acrotag_FrontVowel ;
МАССР:МАССР  acrotag_FrontSon ;
ОМОН:ОМОН acrotag_BackSon ;
ДЮСШ:ДЮСШ acrotag_BackObstr ;

LEXICON acr2
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ|а|б|в|г|д|е|ж|з|и|й|к|л|м|н|о|п|р|с|т|у|ф|х|ц|ч|ш|щ|ъ|ы|ь|э|ю|я|ё|ҥ|ӓ|ӱ|ӹ|ӧ] > acr3 ;
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ|а|б|в|г|д|е|ж|з|и|й|к|л|м|н|о|п|р|с|т|у|ф|х|ц|ч|ш|щ|ъ|ы|ь|э|ю|я|ё|ҥ|ӓ|ӱ|ӹ|ӧ] > acr5 ;

LEXICON acr3
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ|а|б|в|г|д|е|ж|з|и|й|к|л|м|н|о|п|р|с|т|у|ф|х|ц|ч|ш|щ|ъ|ы|ь|э|ю|я|ё|ҥ|ӓ|ӱ|ӹ|ӧ] > acr4 ;
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ|а|б|в|г|д|е|ж|з|и|й|к|л|м|н|о|п|р|с|т|у|ф|х|ц|ч|ш|щ|ъ|ы|ь|э|ю|я|ё|ҥ|ӓ|ӱ|ӹ|ӧ] > acr5 ;

LEXICON acr4
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|Л|М|Н|О|П|Р|С|Т|У|Ф|Х|Ц|Ч|Ш|Щ|Ъ|Ы|Ь|Э|Ю|Я|Ё|Ҥ|Ӓ|Ӱ|Ӹ|Ӧ|а|б|в|г|д|е|ж|з|и|й|к|л|м|н|о|п|р|с|т|у|ф|х|ц|ч|ш|щ|ъ|ы|ь|э|ю|я|ё|ҥ|ӓ|ӱ|ӹ|ӧ] > acr5 ;

LEXICON acr5
< [О|У|Ю|Ё] > acrotag_BackVowel ;
< [А|Б|В|Г|Д|Е|Ж|З|И|Й|К|П|Т|Х|Ц|Ч|Ш|Щ|Ы|Э|Я|Ӓ|Ӹ] > acrotag_FrontVowel ;
< [Ӱ|Ӧ] > acrotag_FrontRoundedVowel ;
< [Л|М|Н|Р|Ҥ] > acrotag_FrontSon ;
< [С|Ф|Ъ|Ь] > acrotag_FrontObstr ;

LEXICON acrotag_BackVowel
+N+ACR:%^VoTrigger ACRONDELIMITER ;

LEXICON acrotag_FrontVowel
+N+ACR:%^VeTrigger ACRONDELIMITER ;

LEXICON acrotag_FrontRoundedVowel
+N+ACR:%^VOTrigger ACRONDELIMITER ;

LEXICON acrotag_FrontSon
+N+ACR:%^VeTrigger%^Sonorant ACRONDELIMITER ;
+Err/Orth+N+ACR:%^VeTrigger ACRONDELIMITER ;

LEXICON acrotag_FrontObstr
+N+ACR:%^VeTrigger%^FrontObstr ACRONDELIMITER ;

LEXICON acrotag_BackSon
+N+ACR:%^VoTrigger%^Sonorant ACRONDELIMITER ;

LEXICON acrotag_BackObstr
+N+ACR:%^VoTrigger%^FrontObstr ACRONDELIMITER ;


LEXICON acrotag
+N+ACR: ACRONDELIMITER ;


LEXICON acros
кг  acrotag ; ! kilogramme
мм acrotag ; ! millimetre
стр acrotag ; ! page
экз acrotag ; ! example
млн acrotag ; ! million
Мф  acrotag ; ! books of the New Testament
Ап  acrotag ; ! books of the New Testament
Лк  acrotag ; ! books of the New Testament
см³ acrotag ; ! 
°C acrotag ;
K acrotag ; ! Kelvin, Latin letter K

