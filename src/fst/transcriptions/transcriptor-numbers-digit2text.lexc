
LEXICON Root
HUNDREDSM ;		! 200M
1:шӱдӧ%  HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 11-19M
TENM ;			! 10M->11M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
1:шӱдӧ%  HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
1:тӱжем%  THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
2:кок   CUODIM ;
3:кум  CUODIM ;
4:ныл  CUODIM ;
5:вич  CUODIM ;
6:куд  CUODIM ;
7:шым  CUODIM ;
8:кандаш  CUODIM ;
9:индеш  CUODIM ;

LEXICON CUODIM
:шӱдӧ%  HUNDREDM ;

LEXICON HUNDREDM
TENSM ;
TEENSM ;
%0: ONESM ;
%0%0: MILJON ;

LEXICON TEENSM
1: TEENM ;

LEXICON TEENM
лат: LOHKAIM ;

LEXICON LOHKAIM
1:ик%   MILJON ;
2:кок%   MILJON ;
3:кум%  MILJON ;
4:ныл%  MILJON ;
5:вич%   MILJON ;
6:куд%  MILJON ;
7:шым%  MILJON ;
8:кандаш%  MILJON ;
9:индеш%  MILJON ;

LEXICON TENSM
2:коло    LÅGEVM ;
3:кумло   LÅGEVM ;
4:нылле   LÅGEVM ;
5:вичле   LÅGEVM ;
6:кудло   LÅGEVM ;
7:шымле   LÅGEVM ;
8:кандашле   LÅGEVM ;
9:индешле   LÅGEVM ;


LEXICON TENM
1%0:лу%  MILJON ;

LEXICON LÅGEVM
!%0:лу%  MILJON ;
!:лу%  ONESM ;
%0:  MILJON ;
:%  ONESM ;

LEXICON ONESM
1:ик% миллион%  OVERTHOUSANDS ;
2:кок%    MILJON ;
3:кум%   MILJON ;
4:ныл%   MILJON ;
5:вич%   MILJON ;
6:куд%   MILJON ;
7:шым%   MILJON ;
8:кандаш%   MILJON ;
9:индеш%   MILJON ;

LEXICON MILJON
:миллион%  OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
1:шӱдӧ%   HUNDREDT ;	! 100.000.000
%0: TENST ;		!  20.000.000
%0: TEENST ;		!  10.000.000
%0%0: ONEST ;		!   2.000.000
%0%01:тӱжем%  THOUSAND ;	!   1.000.000
%0%0%0: UNDERTHOUSAND ;	
%0%0%0%0: TENS ;
%0%0%0%0: TEENS ;
%0%0%0%0%0: ONES ;
%0%0%0%0%0%0: # ;	!  x000000

LEXICON HUNDREDST
2:кок   CUODIT ;	! 200000-299999
3:кум  CUODIT ;	! 300000-399999
4:ныл  CUODIT ;	! 400000-499999
5:вич  CUODIT ;	! 500000-599999
6:куд  CUODIT ;	! 600000-699999
7:шым  CUODIT ;	! 700000-799999
8:кандаш  CUODIT ;	! 800000-899999
9:индеш  CUODIT ;	! 900000-999999

LEXICON CUODIT
:шӱдӧ%   HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
%01:икыт  THOUSANDS ;
%0: ONEST ;		! XX0YYY
%0%0: THOUSANDS ;	! X00YYY

LEXICON TEENST
1: TEENT ;

LEXICON TEENT
:лат  LOHKAIT ;
%0:лу THOUSANDS ;

LEXICON LOHKAIT
!%0:лу  THOUSANDS ;
1:ик%  THOUSANDS ;
2:кок%  THOUSANDS ;
3:кум%  THOUSANDS ;
4:ныл%  THOUSANDS ;
5:вич%  THOUSANDS ;
6:куд%  THOUSANDS ;
7:шым%  THOUSANDS ;
8:кандаш%  THOUSANDS ;
9:индеш%  THOUSANDS ;

LEXICON TENST
!2:кок  THOUSANDSS ;
2:коло  THOUSANDSS ;
3:кумло  THOUSANDSS ;
4:нылле  THOUSANDSS ;
5:вичле  THOUSANDSS ;
6:кудло  THOUSANDSS ;
7:шымле  THOUSANDSS ;
8:кандашле  THOUSANDSS ;
9:индешле  THOUSANDSS ;

LEXICON THOUSANDSS
!%0:лу%  THOUSANDS ;
!1:луикыт THOUSAND ;
%0:%  THOUSANDS ;
:%   ONEST ;

LEXICON ONEST
1:икыт%  THOUSANDS ;
2:кок%  THOUSANDS ;
3:кум%  THOUSANDS ;
4:ныл%  THOUSANDS ;
5:вич%  THOUSANDS ;
6:куд%  THOUSANDS ;
7:шым%  THOUSANDS ;
8:кандаш%  THOUSANDS ;
9:индеш%  THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
:тӱжем%  THOUSAND ;

LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
%0: TENS ;	   ! 1020-1099
%0: TEENS ;	   ! 1010-1019
%0%0: ONES ;	   ! 1001-1009
%0%0%0: # ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
1:шӱдӧ%  HUNDRED ;

LEXICON HUNDREDS
2:кок   CUODI ;
3:кум   CUODI ;
4:ныл   CUODI ;
5:вич   CUODI ;
6:куд   CUODI ;
7:шым   CUODI ;
8:кандаш   CUODI ;
9:индеш   CUODI ;

LEXICON CUODI
:шӱдӧ%  HUNDRED ;

LEXICON HUNDRED
TENS ;
TEENS ;
%0: ONES ;
%0%0: # ;

LEXICON TEENS
1: TEEN ;

LEXICON TEEN
:лат LOHKAI ;

LEXICON LOHKAI
1:икыт # ;
2:кокыт # ;
3:кумыт # ;
4:нылыт # ;
5:визыт # ;
6:кудыт # ;
7:шымыт # ;
8:кандаше # ;
9:индеше  # ;

LEXICON TENS
1%0:лу # ;
2:коло%   LÅGEV ;
3:кумло%   LÅGEV ;
4:нылле%   LÅGEV ;
5:витле%   LÅGEV ;
6:кудло%   LÅGEV ;
7:шымле%   LÅGEV ;
8:кандашле%   LÅGEV ;
9:индешле%   LÅGEV ;

LEXICON LÅGEV
%0: # ;
    ONES ;

LEXICON ONES
1:икыт # ;
2:кокыт # ;
3:кумыт # ;
4:нылыт # ;
5:визыт # ;
6:кудыт # ;
7:шымыт # ;
8:кандаше # ;
9:индеше  # ;

