!! Noun inflection
!! ---------------
!! Meadow Mari nouns inflect in cases.

LEXICON N
+N: N-continuation ;

LEXICON N-continuation 
!Lex0 ; 
K ; C ; S1 ; S2 ; S5 ; S6 ; P2 ; P4 ; P5 ; L ;

LEXICON N-a/e
+N: N-a/e-continuation ;

LEXICON N-a/e-continuation
!Lex0 ; 
K ; C ; S1 ; S2 ; S4 ; S6 ; P1 ; P4 ; P5 ; L ;

LEXICON N-VS
+N: N-VS-continuation ;

LEXICON N-VS-continuation
!Lex0 ; 
K ; C ; S1 ; S2 ; S3 ; S5 ; S6 ; P2 ; P3 ; P4 ; P5 ; L ;

LEXICON S  
Lex0 ; K ; C ; P2 ; P4 ; P5 ;

LEXICON S-a  
Lex0 ; K ; C ; P1 ; P4 ; P5 ;

LEXICON S-VS
Lex0 ; K ; C ; P2 ; P3 ; P5 ;

LEXICON Lx  
Lex0 ; K ; C ; S1 ; S2 ; S5 ; S6 ; P2 ; P4 ; P5 ;

LEXICON L-a 
Lex0 ; K ; C ; S1 ; S2 ; S4 ; S6 ; P1 ; P4 ; P5 ;

LEXICON P 
Lex0 ; K ; C ; S1 ; S2 ; S5 ; S6 ; L ; W5 ;

LEXICON P-a
Lex0 ; K ; C ; S1 ; S2 ; S4 ; S6 ; L ; W5 ;

LEXICON P-Z
S6 ; W5 ;


! Nominal affixes
! ----------------

! Case

LEXICON S1
+Nom: KO ;
+Gen:X1X3н K0 "GEN" ;
+Acc:X1X3м K0 "AKK" ;
+Com:X3ге K0 "KOM" ;

LEXICON S2
+Ill:X1X3ш S-VS "ILL" ;
+Ill:X1X3шке1 S "ILL" ;
+Ine:X1X3ште1 S "IN" ;

LEXICON S3
+Ill:X3ке1 S "ILL" ;
+Ine:X3те1 S "IN" ;

LEXICON S4
+Lat:X3ш S-VS "LAT" ;

LEXICON S5
+Lat:X3еш S-VS "LAT" ;

LEXICON S6
+Dat:X3лан S "DAT" ;
+Cmp:X3ла S-a "KOMP" ;


! Number

LEXICON L
+Pl:X4-влак Lx "PL" ; ! earlier L
+Pl:X4-шамыш Lx "PL-SamYC" ; ! earlier L
+Pl:X4ла L-a "PL-la" ;
+Pl:X4мыт Lx "PL-mYt" ; ! earlier L


! Possessive Person

LEXICON P1
+PxSg1:X1X5м P "PX-SG-1" ;
+PxSg2:X1X5т P "PX-SG-2" ;

LEXICON P2
+PxSg1:X5ем P "PX-SG-1" ;
+PxSg2:X5ет P "PX-SG-2" ;

LEXICON P3
+PxSg3:X5Yже1 P "PX-SG-3" ;

LEXICON P4
+PxSg3:X5же1 P "PX-SG-3" ;

LEXICON P5
+PxSg3:X1X5ж P-Z "PX-SG-3" ;
+PxPl1:X5на P-a "PX-PL-1" ;
+PxPl2:X5да P-a "PX-PL-2" ;
+PxPl3:X1X5шт P "PX-PL-3" ;
+PxSg3:X5ждене K0 "PX-SG-3+dene" ;
+PxSg3:X5ждеке K0 "PX-SG-3+deke" ;
+PxSg3:X5ждеч K0 "PX-SG-3+deC" ;



! vim: set ft=xfst-lexc:

