! =====================================!
! Orthographic converter for Meadow Mari !
! =====================================!

! This program translates text written in the orthographies
! of the 20s and 30s into modern orthography

echo <<defining Cns and Vow...>>


define Vow [ а | е | и | э | я | о | у | ё | ю | ӧ | ӱ | А | Е | И | Э | Я | О | У | Ё | Ю | Ӧ | Ӱ ] ; 

define Vow2 [ а | э | у | А | Э | У ] ; 

define Cns  [ б | в | г | д | ж | з | й | к | л | м | н | ҥ | п | р | с | т | ф | х | ц | ч | ш | щ | 
              Б | В | Г | Д | Ж | З | Й | К | Л | М | Н | Ҥ | П | Р | С | Т | Ф | Х | Ц | Ч | Ш | Щ ] ;
   
define Signs [ ъ | ь | Ъ | Ь ] ;

echo << Rules>>


define CnsJV й -> ъ й , Й -> Ъ Й || Cns _ Vow2 ;  # пӧрйэҥ -> пӧръйэҥ

define JE  й э -> е , ь э -> е , Й [Э|э] -> Е , Ь Э -> Е ; # пӧръйэҥ -> пӧръеҥ
define JA  й а -> я , ь а -> я , Й [А|а] -> Я , Ь А -> Я ;
define JU  й у -> ю , ь у -> ю , Й [У|у] -> Ю , Ь У -> Ю ;

define CnsE э -> е , Э -> Е || Cns _ ;


echo << Combining >>

read regex [ CnsJV
.o. JE 
.o. JA 
.o. JU 
.o. CnsE ] ;

# cd src/orthography/
# xfst -e " source < oldorthography2norm.xfscript "
# invert net
# save o2n.fst
# quit

# Тӱньа ӱмбал шэмэр калыкын пагалымэ, йӧратымэ вуйлатышыжэ ВЛАДИМИР ИЛЬИЧ ЛЕНИН укэ лийэ.

# пӧрйеҥ мутйарым ыштышйӧн

