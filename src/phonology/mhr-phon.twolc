!! !!!Mari twol file

!! This file contains rules for morphophonological alternations, such as
!! vowel harmony, stem vowel changes, palatalisation, etc.

!! We define our symbols (__Alphabet__), some __Sets__, and then the __Rules__

Alphabet

!! !!Letters of the alphabet
а б в г д е ё ж з и й к л м н ҥ о ӧ п р с т у ӱ ф х ц ч ш щ ъ ы ь э ю я ӓ ӹ !!= * @CODE@
А Б В Г Д Е Ё Ж З И Й К Л М Н Ҥ О Ӧ П Р С Т У Ӱ Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӓ Ӹ !!= * @CODE@

!! other symbols
   %- !!= * @CODE@

!! Archiphonemes for vowels, Giellatekno style
  е1:е а1:а и1:и у1:у ӱ1:ӱ я1:я  !!= * @CODE@ 
  Ы1:ы Ы1:0
 Ы2:ы Ы2:0   !!= * @CODE@ Variation in %{еоыӧØ%}
  А1:а А1:ӓ   !!= * @CODE@ 
  Е2:е Е2:0 А2:а А2:я  !!= * @CODE@ 

!! Archiphonemes for vowels, Apertium style
%{аы%}:а !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{аы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{еы%}:е !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{еы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{оы%}:о !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{оы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{ӧы%}:ӧ !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{ӧы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel

%{яы%}:я !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{яы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel

%{еоыӧØ%}:е  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:о  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:ӧ  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:0  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:ы  !!= * @CODE@ PxSg3 final
%{ыØ%}:ы  !!= * @CODE@ PxSg3 onset
%{ыØ%}:0  !!= * @CODE@ PxSg3 onset

%{ьØ%}:0  !!= * @CODE@ for -ам verbs Prt1 Sg1, Sg2, Sg3, Pl3 л н

 Е1:е !!= * @CODE@ for lative

!  Е1:Е А1:А И1:И У1:У Ӱ1:Ӱ
!JMR 2012-09-18

! Boundary symbols
! old !  +  /  {  _  $  }  @  ~  &  # (old = Jorma 1985)

! JMR 2012-09-18
 %>   !!= * @CODE@ 


 ;


!! !!Sets
Sets

Vo =   о у ё ю О У Ё Ю ;   !!= * @CODE@ 
VO =   ӧ ӱ Ӧ Ӱ;              !!= * @CODE@ 
Ve =   а е и э я А Е И Э Я ;  !!= * @CODE@ 
Vws =  Vo VO Ve ы Ы ;   !!= * @CODE@ 

Cns =  б в г д ж з к л м н ҥ п р с т ф х ц ч ш   !!= * @CODE@ 
       Б В Г Д Ж З К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш ;    !!= @CODE@ 
Cst =  к п т ф х ц ч ш с      !!= * @CODE@ 
       К П Т Ф Х Ц Ч Ш Ц ;    !!= @CODE@ 

Ltrs = Vws Cns  ъ ь Ъ Ь ;   !!= * @CODE@ 

all =  Ltrs  %- ;    !!= * @CODE@ 



Definitions



!! !!Rules
Rules


"Palatal mark loss before vowel"
!! __ @RULENAME@ __ 
    ь:0  <=>   _ %>: А2: ;
               _ %{яы%}: .#. ;
!!€ башнь%{яы%}
!!€ башн0я

      

! JMR 2012-09-18
"Onset vowel loss in suffix"
!! __ @RULENAME@ __  
 Vx:0 <=>        :Vws %> _ ;
           %> ж Ы2: %> _ ;
        where Vx in (Е2 Ы1) ;

"Onset vowel Е2 realized in suffix"
!! __ @RULENAME@ __  
  Е2:е <=> Cns %> _ ;

"Onset vowel Ы1 realized in suffix"
!! __ @RULENAME@ __  
  Ы1:ы <=> [ Cns | й ] %> _ ;


"suffix-final vowel loss after stem-final vowel"
!! __ @RULENAME@ __  
  Ы2:0 <=> Vws %> [ Ы1: ш [ т | к ] | ж: ] _ %> [ л | г | ш | д | Е2: | А2: ] ;
! Vws %> [ Ы1: ш [ т | к ] | ж: ] _ %> [ л | г | Ы1: ш | ш | д | Е2: | А2: ] ;
           Cns %> [ Ы1: ш [ т | к ] | ж: ] _ %> [ е ш |Е1: ш | А2: ] ;

"suffix-final vowel realized after stem-final consonant"
!! __ @RULENAME@ __  
  Ы2:ы <=>  Cns %> [ ж: | д ы м ] _ %> [ л | г | ш [ т | к ] | д | Ы1: ] ;
            Vws %> [ ж | д ы м ] _ %> Ы1: [ м | н ] ;
!            %>  Ы1: ш [ т | к ]  _ %> Ы1: ;

"suffix-final vowel realized as -Round in word-final position"
!! __ @RULENAME@ __  
  Ы2:е <=> :Ve [ Cns | й | :ы | :0 ]* %> (%{ьØ%}:) ( ( ж: [ Ы2:е | Ы2:ы | Ы2:0 ] %> ) [ Ы1: | ы ] ш [ т | к ] | ы ж | ж: | ш | м | д ы м ) _ [ .#. | %- ] ;
!           :Ve [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;

"suffix-final vowel realized as +Back +Round in word-final position"
!! __ @RULENAME@ __  
  Ы2:о <=> :Vo [ Cns | :ы | :0 ]* %> (%{ьØ%}:) ( ( ж: [ Ы2:о | Ы2:ы | Ы2:0 ] %> ) Ы1: ш [ т | к ] | ы ж | ж: | ш | м | д ы м ) _ [ .#. | %- ] ;
!           :Vo [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;

"suffix-final vowel realized as +Front +Round in word-final position"
!! __ @RULENAME@ __  
  Ы2:ӧ <=> :VO [ Cns | :ы | :0 ]* %> (%{ьØ%}:) ( ( ж: [ Ы2:ӧ | Ы2:ы | Ы2:0 ] %> ) Ы1: ш [ т | к ] | ы ж | ж: | ш | м | д ы м ) _ [ .#. | %- ] ;
!           :VO [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;


"%{ьØ%}:ь"
!! __ @RULENAME@ __
 %{ьØ%}:ь <=> [н|л] %> _ ;
!! толам+V+Ind+Prt1+Sg1
!!€ тол%>%{ьØ%}ым
!!€ тол%>ьым

! Retaining stem-final _е1_ in adjectives and nouns
! It has been removed from Vem stems
! _е1_ follows much the same pattern as _Ы2_

"suffix-final vowel realized after stem-final consonant"
!! __ @RULENAME@ __  
  е1:ы <=>  Cns _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized as -Round in word-final position"
!! __ @RULENAME@ __  
  е1:е <=> Ve [ Cns | ы ]* Cns _ [ .#. | %- ] ;

"stem-final vowel realized as +Back +Round in word-final position"
!! __ @RULENAME@ __  
  е1:о <=> Vo [ Cns | ы ]* _ [ .#. | %- ] ;

"stem-final vowel realized as +Front +Round in word-final position"
!! __ @RULENAME@ __  
  е1:ӧ <=> VO [ Cns | ы ]* _ [ .#. | %- ] ;


"suffix-final vowel realized %{аы%}:ы "
!! __ @RULENAME@ __  
  %{аы%}:ы <=>  Cns _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized %{аы%}:а"
!! __ @RULENAME@ __  
  %{аы%}:а <=> Cns _ [ .#. | %- ] ;

"suffix-final vowel realized %{еы%}:ы "
!! __ @RULENAME@ __  
  %{еы%}:ы <=>  [Cns (ь)| Vow 0: ] _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized %{еы%}:е"
!! __ @RULENAME@ __  
  %{еы%}:е <=> Cns (ь) _ [ .#. | %- ] ;

"suffix-final vowel realized %{ӧы%}:ы "
!! __ @RULENAME@ __  
  %{ӧы%}:ы <=>  [Cns (ь)| Vow 0: ] _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized %{ӧы%}:ӧ"
!! __ @RULENAME@ __  
  %{ӧы%}:ӧ <=> Cns (ь) _ [ .#. | %- ] ;

"suffix-final vowel realized %{оы%}:ы "
!! __ @RULENAME@ __  
  %{оы%}:ы <=>  Cns _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized %{оы%}:о"
!! __ @RULENAME@ __  
  %{оы%}:о <=> Cns _ [ .#. | %- ] ;

"suffix-final vowel realized %{яы%}:ы "
!! __ @RULENAME@ __  
  %{яы%}:ы <=>  [Cns ь| Vws й] _ %>: [ л | г | ш | д | Ы1: ] ;
        
"stem-final vowel realized %{яы%}:я"
!! __ @RULENAME@ __  
  %{яы%}:я <=> [Cns ь:| Vws 0:] _ [ .#. | %- ] ;

!!€ башнь%{яы%}
!!€ башн0я

"stem-internal glide realized in  0:й %{яы%}:ы "
!! __ @RULENAME@ __
  0:й <=> Vws _ [ %{яы%}:ы | %{еы%}:ы ] ;

!!€ а0%{яы%}%>Ы1м
!!€ айы%>0м



!2017-01-03!!begin %{еоӧØ%}
!2017-01-03!"suffix-final vowel loss after stem-final vowel %{еоыӧØ%} to ZERO"
!2017-01-03!!! __ @RULENAME@ __  
!2017-01-03!  %{еоыӧØ%}:0 <=> Vws %> [ Ы1: ш [ т | к ] | ж: ] _ %> [ л | г | Ы1: ш | ш | д | Е2: | А2: ] ;
!2017-01-03!           Cns %> [ Ы1: ш [ т | к ] | ж: ] _ %> [ е ш | А2: ] ;
!2017-01-03!
!2017-01-03!"suffix-final vowel realized after stem-final consonant %{еоыӧØ%} to ы "
!2017-01-03!!! __ @RULENAME@ __  
!2017-01-03!  %{еоыӧØ%}:ы <=>  Cns %> [ ж: | д ы м ] _ %> [ л | г | ш [ т | к ] | д | Ы1: ] ;
!2017-01-03!            Vws %> [ ж | д ы м ] _ %> Ы1: [ м | н ] ;
!2017-01-03!!            %>  Ы1: ш [ т | к ]  _ %> Ы1: ;
!2017-01-03!
!2017-01-03!"suffix-final vowel realized as -Round in word-final position %{еоыӧØ%} 2 е"
!2017-01-03!!! __ @RULENAME@ __  
!2017-01-03!  %{еоыӧØ%}:е <=> :Ve \[ :Vo | :VO ]* %> ( [ Ы1: | ы ] ш [ т | к ] | ы ж | ж: | ш | м | д ы м ) _ [ .#. | %- ] ;
!2017-01-03!           :Ve \[ :Vo | :VO ]* %> %{ыØ%}:0 [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;
!2017-01-03!!           :Ve [ Cns | :ы | :0 ]* %> %{ыØ%}:0 [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;
!2017-01-03!
!2017-01-03!"suffix-final vowel realized as +Back +Round in word-final position %{еоыӧØ%} 2 о "
!2017-01-03!!! __ @RULENAME@ __  
!2017-01-03!  %{еоыӧØ%}:о <=>
!2017-01-03! :Vo [ Cns | :ы | :0 ]* %> ( ( ж: [ %{еоыӧØ%}:о | %{еоыӧØ%}:ы | %{еоыӧØ%}:0 ] %> ) Ы1: ш [ т | к ] | ы ж | ж: | ш | м | д ы м ) _ [ .#. | %- ] ;
!2017-01-03!           :Vo [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;
!2017-01-03!
!2017-01-03!"suffix-final vowel realized as +Front +Round in word-final position %{еоыӧØ%} 2 ӧ "
!2017-01-03!!! __ @RULENAME@ __  
!2017-01-03!  %{еоыӧØ%}:ӧ <=>
!2017-01-03! :VO [ Cns | :ы | :0 ]* %> ( ( ж: [ %{еоыӧØ%}:ӧ | %{еоыӧØ%}:ы | %{еоыӧØ%}:0 ] %> ) Ы1: ш [ т | к ] | ы ж | ж: | ш | м | д ы м ) _ [ .#. | %- ] ;
!2017-01-03!           :VO [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;
!2017-01-03!
!2017-01-03!
!end %{еоӧØ%}

"Clitics in At and Ak take onset glide = a"
!! __ @RULENAME@ __  
  А2:а <=> [ Cns ( Ы2:0 ) | :Vws ] %> _ [ т | к | м | ш | .#. ] ;
 
"Clitics in At and Ak take onset glide = ja"
!! __ @RULENAME@ __  
  А2:я <=> ь:0 %> _ [ т | к | м | ш ] ;
! "A2 as я in й-final stems"
           й:0 %> _  ;


"й Deletion in front of я Suffix and others"
!! __ @RULENAME@ __  
  й:0 <=> _ %>: [ :я | :е | :и | :ю ]  ;
          _     [ :я | :е | :и | :ю ] [ .#. | %- ] ;

!!€ кий>А2ш
!!€ ки0>яш

!"йа suffix as я 1"
!! __ @RULENAME@ __
!й:0 <=> Vow _ %>: а: ;

!"йа suffix as я 2"
!! __ @RULENAME@ __
!а:я <=> Vow й: %>: _ ;

!!€ кай>А2
!!€ ка0>я 

!!€ кутыр>А2
!!€ кутыр>а 



! Devoicing
"Onset consonant devoicing"
!! __ @RULENAME@ __
ж:ш <=> [ Cst | ж ] %> _ Ы2: ;
         [ Cst | ж ] %> %{ыØ%}:0 _ %{еоыӧØ%}: ;  



"Disallow PxSg3 in ыж everywhere except after ш "
!! __ @RULENAME@ __
ы:ы /<= [Ltrs - ш] %> _ ж [.#.| %-]  ;
!!€ йолташ%>ыж#
!!€ йолташ%>ыж#
!!$ олма%>ыж#
!!$ олма%>ыж#



! just to see whether other tests pass:
!!€ а
!!€ б
