!! !!!Mari twol file

!! This file contains rules for morphophonological alternations, such as
!! vowel harmony, stem vowel changes, palatalisation, etc.

!! We define our symbols (__Alphabet__), some __Sets__, and then the __Rules__

Alphabet

!! !!Letters of the alphabet
а б в г д е ё ж з и й к л м н ҥ о ӧ п р с т у ӱ ф х ц ч ш щ ъ ы ь э ю я ӓ ӹ !!= * @CODE@
А Б В Г Д Е Ё Ж З И Й К Л М Н Ҥ О Ӧ П Р С Т У Ӱ Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Ӓ Ӹ !!= * @CODE@

!! other symbols
•:0
ʼ:0
   %- !!= * @CODE@

!! Archiphonemes for vowels, Giellatekno style
  е1:е а1:а и1:и у1:у ӱ1:ӱ я1:я  !!= * @CODE@ 
  Ы1:ы Ы1:0
 Ы2:ы Ы2:0   !!= * @CODE@ Variation in еоыӧØ
  А1:а А1:ӓ   !!= * @CODE@ 
  Е2:е Е2:0 А2:а А2:я  !!= * @CODE@ 

!! Archiphonemes for vowels, Apertium style
%{аы%}:а !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{аы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{еы%}:е !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{еы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{оы%}:о !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{оы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{ӧы%}:ӧ !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{ӧы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel

%{яы%}:я !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel
%{яы%}:ы !!= * @CODE@ Stem-final vowel variation when stress falls on non-final vowel

%{еоыӧØ%}:е  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:о  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:ӧ  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:0  !!= * @CODE@ PxSg3 final
%{еоыӧØ%}:ы  !!= * @CODE@ PxSg3 final
%{ыØ%}:ы  !!= * @CODE@ PxSg3 onset
%{ыØ%}:0  !!= * @CODE@ PxSg3 onset

%{ьØ%}:0  !!= * @CODE@ for -ам verbs Prt1 Sg1, Sg2, Sg3, Pl3 л н


 Е1:е !!= * @CODE@ for lative

!  Е1:Е А1:А И1:И У1:У Ӱ1:Ӱ
!JMR 2012-09-18

! Boundary symbols
%^END:0		  !!= * @CODE@ for -ам verb final, i.e. Imprf
! old !  +  /  {  _  $  }  @  ~  &  # (old = Jorma 1985)

! JMR 2012-09-18
 %>   !!= * @CODE@ 


 ;


!! !!Sets
Sets

Vo =   о у ё ю О У Ё Ю ;   !!= * @CODE@ 
VO =   ӧ ӱ Ӧ Ӱ;              !!= * @CODE@ 
Ve =   а е и э я А Е И Э Я ;  !!= * @CODE@ 
Vow =  Vo VO Ve ы Ы ;   !!= * @CODE@ 

Cns =  б в г д ж з к л м н ҥ п р с т ф х ц ч ш   !!= * @CODE@ 
       Б В Г Д Ж З К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш ;    !!= @CODE@ 
Cst =  б в г д ж з к п с т ф х ц ч ш      !!= * @CODE@ 
       Б В Г Д Ж З К П С Т Ф Х Ц Ч Ш ;    !!= @CODE@ 

Ltrs = Vow Cns  ъ ь Ъ Ь ;   !!= * @CODE@ 

all =  Ltrs  %- ;    !!= * @CODE@ 



Definitions

BackRounded = [ :Vo (•:) [ Cns: |й:|(ʼ:) ь:|%{ьØ%}:| :ы | %> | Ы1: | Ы2:о| Ы2:ы]*  ] ;

FrontRounded = [:VO  (•:) [ Cns: |й:|(ʼ:) ь:|%{ьØ%}:| :ы | %> | Ы1: | Ы2:ӧ| Ы2:ы]* ] ;

FrontUnrounded = [ [:Ve (•:) |ы •:] [ Cns: |й:|(ʼ:) ь:|%{ьØ%}:| :ы | %> | Ы1: | Ы2:е| Ы2:ы]*  ] ;

!! !!Rules
Rules


"Palatal mark loss before vowel"
!! __ @RULENAME@ __ 
    ь:0  <=>   _ (%{еы%}:|%{оы%}:|%{ӧы%}:) %> [А2:|Е2:|е] ;
!! имне+N+Sg+Nom+Foc/Ат
!!€ имнь%{еы%}%>А2т
!!€ имн00%>ят
               _ %{яы%}: .#. ;
!!€ башнь%{яы%}
!!€ башн0я
     _ %{еы%}: [# | %-] ;


! JMR 2012-09-18
"Onset vowel loss in suffix after stem vowel"
!! __ @RULENAME@ __  
 Ы1:0 <=>  [:Vow (•:)|%{еы%}:|%{оы%}:|%{ӧы%}:] %> _ ;
           %> ж Ы2: %> _ ;

"Onset vowel Е2 realized in suffix е"
!! __ @RULENAME@ __  
  Е2:е <=> [Cns ((ʼ:) ь:)|й:|%{еы%}:|%{оы%}:|%{ӧы%}:] %> _ ;

"Onset vowel Е2 realized in suffix э"
!! __ @RULENAME@ __  
  Е2:э <=> [у|ӱ|и|ю|о|ӧ] (•:) %> _ ;

"Onset vowel Е2 realized in suffix ZERO"
!! __ @RULENAME@ __  
  Е2:0 <=> [а|ӓ|я|е|э] (•:) %> _ ;

"Onset vowel Ы1 realized in suffix"
!! __ @RULENAME@ __  
  Ы1:ы <=> [ Cns ((ʼ:) ь:) | й ] %> _ ;


"suffix-final vowel loss after stem-final vowel"
!! __ @RULENAME@ __  
  Ы2:0 <=> Vow (•:) %> Ы1: ш [ т | к ]  _ %> [ л | г | ш | д | Е2: | А2: ] ;
           Cns %> Ы1: ш [ т | к ]  _ %> [ е ш |Е1: ш | А2: ] ;
                                   _ %> [Е2:|А2:] ;
                  .#. (Cns) [у|ӱ|ӧ|о|е|э] (•:) %> _ %^END:  ; ! пуаш : пу

!!€ пу>Ы2%^END
!!€ пу>00

"suffix-final vowel realized after stem-final consonant"
!! __ @RULENAME@ __  
  Ы2:ы <=>  Cns %> [ ж: | д ы м ] _ %> [ л | г | ш [ т | к ] | д | Ы1: ] ;
            Vow (•:) %> [ ж | д ы м ] _ %> Ы1: [ м | н ] ;
!            %>  Ы1: ш [ т | к ]  _ %> Ы1: ;

"suffix-final vowel realized as -Round in word-final position е "
!! __ @RULENAME@ __  
  Ы2:е <=>
  FrontUnrounded %> (%{ьØ%}:) [ ( ж: [ Ы2:е | Ы2:ы | Ы2:0 ] %> ) [ Ы1: | ы ] ш [ т | к ] | ы ж | ж: | д| ш | м | д ы м ] _ [ .#. |%^END:| %- ] ;
  FrontUnrounded %> %{ьØ%}: _ [ .#. |%^END:| %- ] ;
 [:Ve (•:) |ы •:] [ Cns: |й:|(ʼ:) ь:|%{ьØ%}:| :ы | %> | Ы1: | Ы2:е| Ы2:ы ]+ %> _ [ .#. |%^END:| %- ] ;
 Vow (•:) Cns:* [:Ve (•:) |ы •:] %> _ [ .#. |%^END:| %- ] ;

  FrontUnrounded [ ш %> т| й:] _ [ .#. | %- ] ;
!           :Ve [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;


"suffix-final vowel realized as +Back +Round in word-final position о "
!! __ @RULENAME@ __  
  Ы2:о <=>
 BackRounded  %> (%{ьØ%}:) [ ( ж: [ Ы2:о | Ы2:ы | Ы2:0 ] %> ) Ы1: ш [ т | к ] | ы ж | ж: | д| ш | м | д ы м ] _ [ .#. |%^END:| %- ] ;
 BackRounded  %> %{ьØ%}:  _ [ .#. |%^END:| %- ] ;
 :Vo (•:) [ Cns: |й:|(ʼ:) ь:|%{ьØ%}:| :ы | %> | Ы1: | Ы2:о| Ы2:ы ]+  %>  _ [ .#. |%^END:| %- ] ;
 Vow (•:) Cns:* :Vo (•:) %>  _ [ .#. |%^END:| %- ] ;

  BackRounded [ш %> т| й:] _ [ .#. | %- ] ;
!           :Vo [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;


"suffix-final vowel realized as +Front +Round in word-final position ӧ "
!! __ @RULENAME@ __  
  Ы2:ӧ <=>
 FrontRounded %> (%{ьØ%}:) [ ( ж: [ Ы2:ӧ | Ы2:ы | Ы2:0 ] %> ) Ы1: ш [ т | к ] | ы ж | ж: | д| ш | м | д ы м ] _ [ .#. |%^END:| %- ] ;
 FrontRounded %> %{ьØ%}: _ [ .#. |%^END:| %- ] ;
 :VO  (•:) [ Cns: |й:|(ʼ:) ь:|%{ьØ%}:| :ы | %> | Ы1: | Ы2:ӧ| Ы2:ы]+ %> _ [ .#. |%^END:| %- ] ;
 Vow (•:) Cns:* :VO (•:) %> _ [ .#. |%^END:| %- ] ;
  FrontRounded  [ш %> т| й:] _ [ .#. | %- ] ;
!           :VO [ Cns | :ы | :0 ]* %> [ ж: ] _ %> Ы1: ш [ .#. | %- | %> ] ;


"%{ьØ%}:ь"
!! __ @RULENAME@ __
 %{ьØ%}:ь <=> [н|л] (ʼ:) %> _ [ы [м|ч]|Ы2:ӧ|Ы2:о ] ;
!! толам+V+Ind+Prt1+Sg1
!!€ тол%>%{ьØ%}ым
!!€ тол%>ьым

! Retaining stem-final _е1_ in adjectives and nouns
! It has been removed from Vem stems
! _е1_ follows much the same pattern as _Ы2_

"suffix-final vowel realized after stem-final consonant"
!! __ @RULENAME@ __  
  е1:ы <=>  Cns _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized as -Round in word-final position"
!! __ @RULENAME@ __  
  е1:е <=> Ve (•:) [ Cns | ы ]* Cns _ [ .#. | %- ] ;

"stem-final vowel realized as +Back +Round in word-final position"
!! __ @RULENAME@ __  
  е1:о <=> Vo (•:) [ Cns | ы ]* _ [ .#. | %- ] ;

"stem-final vowel realized as +Front +Round in word-final position"
!! __ @RULENAME@ __  
  е1:ӧ <=> VO (•:) [ Cns | ы ]* _ [ .#. | %- ] ;


"suffix-final vowel realized %{аы%}:ы "
!! __ @RULENAME@ __  
  %{аы%}:ы <=>  Cns _ %>: [ л | г | ш | д | Ы1: ] ;

"stem-final vowel realized %{аы%}:а"
!! __ @RULENAME@ __  
  %{аы%}:а <=> Cns _ [ .#. | %- ] ;

"Stem-final non-stressed vowel loss"
!! __ @RULENAME@ __
 Vx:0 <=>        _ %> [Е2:|А2:] ;
        where Vx in (%{еы%} %{оы%} %{ӧы%}) ;


"suffix-final vowel realized %{еы%}:ы "
!! __ @RULENAME@ __  
  %{еы%}:ы <=>  [Cns ((ʼ:) ь)| Vow (•:) 0: ] _ %> [ л | г | ш | ж | д | Ы1: ] ;
!! имне+N+Sg+PxSg3+Nom horse/hevonen
!!€ имнʼь%{еы%}%>жЫ2
!!€ имн0ьы%>же

"stem-final vowel realized %{еы%}:е"
!! __ @RULENAME@ __  
  %{еы%}:е <=> Cns ((ʼ:) ь:) _ [ .#. | %- ] ;

"suffix-final vowel realized %{ӧы%}:ы "
!! __ @RULENAME@ __  
  %{ӧы%}:ы <=>  [Cns ((ʼ:) ь)| Vow (•:) 0: ] _ %> [ л | г | ш | ж | д | Ы1: ] ;

"stem-final vowel realized %{ӧы%}:ӧ"
!! __ @RULENAME@ __  
  %{ӧы%}:ӧ <=> Cns ((ʼ:) ь) _ [ .#. | %- ] ;

"suffix-final vowel realized %{оы%}:ы "
!! __ @RULENAME@ __  
  %{оы%}:ы <=>  Cns ((ʼ:) ь:) _ %> [ л | г | ш | ж | д | Ы1: ] ;

"stem-final vowel realized %{оы%}:о"
!! __ @RULENAME@ __  
  %{оы%}:о <=> Cns ((ʼ:) ь:) _ [ .#. | %- ] ;

"suffix-final vowel realized %{яы%}:ы "
!! __ @RULENAME@ __  
  %{яы%}:ы <=>  [Cns (ʼ:) ь| Vow (•:) й] _ %>: [ л | г | ш | д | Ы1: ] ;
        
"stem-final vowel realized %{яы%}:я"
!! __ @RULENAME@ __  
  %{яы%}:я <=> [Cns (ʼ:) ь:| Vow (•:) 0:] _ [ .#. | %- ] ;

!!€ башнь%{яы%}
!!€ башн0я

"stem-internal glide realized in  0:й %{яы%}:ы "
!! __ @RULENAME@ __
  0:й <=> Vow (•:) _ [ %{яы%}:ы | %{еы%}:ы ] ;

!!€ а0%{яы%}%>Ы1м
!!€ айы%>0м




"Clitics in At and Ak take onset glide = a"
!! __ @RULENAME@ __  
  А2:а <=> [ Cns ( Ы2:0 |%{еы%}:|%{оы%}:|%{ӧы%}:)| Е2:е|Е2:э|[е|э|о|ӧ|ӱ|и|ю|у] (•:) ] %> _ [ т | к | м | ш | .#. ] ;
 
"Clitics in At and Ak take onset glide = ja"
!! __ @RULENAME@ __  
  А2:я <=> Cns (ʼ:) ь: (%{еы%}:0|%{оы%}:0|%{ӧы%}:0) %> _ [ т | к | м | ш | # ] ;
!! когыльо+N+Sg+Nom+Foc/Ат
!!€ когыль%{оы%}%>А2т
!!€ когыл00%>ят

! "A2 as я in й-final stems"
           й:0 (%{еы%}:|%{оы%}:|%{ӧы%}:) %> _  ;

"Clitics in At and Ak take ZERO"
!! __ @RULENAME@ __
  А2:0 <=> [:а|:я] (•:) %> _ [ т | к | м | ш ] ;



"й Deletion in front of я Suffix and others"
!! __ @RULENAME@ __  
  й:0 <=> _ %>: (:0) [ :я | :е | :и | :ю ]  ;
          _     (:0) [ :я | :е | :и | :ю ] [ .#. | %- ] ;

!!€ кий>А2ш
!!€ ки0>яш

!"йа suffix as я 1"
!! __ @RULENAME@ __
!й:0 <=> Vow (•:) _ %>: а: ;

!"йа suffix as я 2"
!! __ @RULENAME@ __
!а:я <=> Vow (•:) й: %>: _ ;

!!€ кай>А2
!!€ ка0>я 

!!€ кутыр>А2
!!€ кутыр>а 



! Devoicing
"Onset consonant devoicing ж:ш "
!! __ @RULENAME@ __
ж:ш <=> Cst (:0)  %> _ [ Ы2: | ы %> ] ;
!!€ авалтымаш%>жы%>ла
!!€ авалтымаш%>шы%>ла

"Onset consonant devoicing з:с "
!! __ @RULENAME@ __
з:с <=> Cst (:0) %> _ :Vow ;

! Consonant loss
"Stem-final consonant loss т"
!! __ @RULENAME@ __
т:0 <=> [ к ] _ [%> [м|ш|д|н|ж:ш|з:с|с]|%^END:| %- я н] ;

"Stem-final consonant loss к"
!! __ @RULENAME@ __
к:0 <=> [ ш |ч ] _ [%> [м|ш|д|н|ж:ш|з:с|с]|%^END:| %- я н] ;

"Stem-final consonant loss н"
!! __ @RULENAME@ __
н:0 <=> _ ч [%> [м|ш|д|н|ж:ш|з:с|с]|%^END:| %- я н] ;

"Stem-final consonant variation з:ч"
з:ч <=> Vow (•:) _  [%> [м|ш|д|н|ж:ш|з:с|с]|%^END:| %- я н] ;


!!€ воз>
!!€ воч>

!!€ воз>са
!!€ воч>са

"Disallow PxSg3 in ж no where except after :Vow "
!! __ @RULENAME@ __
ж:ж /<= [:Ltrs - :Vow] %> _ [.#.| %-|%>]  ;
!!€ ава%>ж%>де
!!€ ава%>ж%>де
!!$ кол%>ж%>де
!!$ кол%>ж%>де

"Disallow Sg+Ill in тЫ2 everywhere except after stem-final ш "
!! __ @RULENAME@ __
т:т /<= [Ltrs - ш ] (•:) %> _ Ы2:  ;
!! йӧратымаш+N+Sg+Ine
!!€ йӧратымаш%>тЫ2
!!€ йӧратымаш%>те


"Disallow PxSg3 in ыж no where except after ш "
!! __ @RULENAME@ __
ы:ы /<=
 [Ltrs - ш] (•:) %> _ ж: (Ы2:) [.#.| %-|%>]  ;
!!€ йолташ%>ыж#
!!€ йолташ%>ыж#
!!$ олма%>ыж#
!!$ олма%>ыж#
 Vow (•:) %> ж: _ %> [д е|л а|г е]  ;
!!€ кол%>жы%>де
!!€ кол%>жы%>де
!!$ ава%>жы%>де
!!$ ава%>жы%>де


! just to see whether other tests pass:
!!€ а
!!€ б
